thing named _hall_note looks like `{!!a hastily scrawled note|_hall_note}` is in _house_front_hall
	do
		if #command is `examine _hall_note`
			say
				`"Mr. Smith, <p>`
				`I could wait no longer for you to arrive. Somehow the Presence has learned of my letter to you and I cannot risk any more delay.`
				`But rest assured, though the heavens may fall into the depths of the abyss and the rising sun burn the sickly moon, all is not lost. <p>`
				`Follow me."`
		if #command is `use _hall_note`
			if _hall_note is in #here
				do
					say `You take the note.`
					move _hall_note to #player

place named _house_front_hall looks like `{Front Hall|_house_front_hall}`
	do
		if #command is #enter
			or #command is `look around`
			then
				say
					if _hall_note is in #here
						`A {!!hastily scrawled note|_hall_note} sits on a small table in the hall.`
					`<p> A dark staircase leads {!!up to the second floor|go up}. <p>`
					`<p> At the {!!east|go east} end of the hall is a door. <p>`
					`<p> The front door is to the {!!west|go west}. <p>`
		if #command is `go west`
			travel to _street_sinclair_front_lawn
		if #command is `go up`
			travel to _house_upstairs

variable named _mural_puzzle_solved is false
variable named _mural_sequence is 0

place named _house_upstairs looks like `{Second Floor Hall|_house_upstairs}`
	do
		if #command is #enter
			or #command is `look around`
			then
				say
					`A {massive mural|_mural} covers the wall at the top of stairs: {The night sky|_mural_sky}, filled with strange constellations, stretches above a {tumultuous sea|_mural_sea}.`
					`On the right, a {flaming red sun|_mural_sun} rises out of the waves. On the left, a {pale yellow moon|_mural_moon} sinks behind the haunting silhouette of an island.`
					`<p> There are doors to the {!!north|go north} and {!!south|go south}. <p>`
					`<p> {!!Stairs|go down} lead back down to the front hall`
		if #command is `go down`
			travel to _house_front_hall
		if #command is `go north`
			do
				if $_mural_puzzle_solved$ is false
					say `The door has no knob, and it won't budge.`
				if $_mural_puzzle_solved$ is true
					travel to _house_laboratory
		if #command is `go south`
			say `The door is locked.`
		if #command is `examine _mural`
			say `Upon closer inspection, you can see that the mural is made up of several large pieces, some of which are slightly raised. <p>`

		if #command is `use _mural_sky`
			do
				say `You push against the starry sky and it gives slightly.`
				if $_mural_sequence$ is 0
					then
						do
							set _mural_sequence to 1
							say `You hear a faint click.`
					else
						do
							set _mural_sequence to 0
							say `You hear a distant clang.`
		if #command is `use _mural_sea`
			do
				say `You push against the raging waves and they gives slightly.`
				if $_mural_sequence$ is 1
					then
						do
							set _mural_sequence to 2
							say `You hear a faint click.`
					else
						do
							set _mural_sequence to 0
							say `You hear a distant clang.`
		if #command is `use _mural_sun`
			do
				say `You push against the burning sun and it gives slightly.`
				if $_mural_sequence$ is 2
					then
						do
							set _mural_sequence to 3
							say `You hear a faint click.`
					else
						do
							set _mural_sequence to 0
							say `You hear a distant clang.`
		if #command is `use _mural_moon`
			do
				say `You push against the pale moon and it gives slightly.`
				if $_mural_sequence$ is 3
					then
						do
							set _mural_sequence to 4
							set _mural_puzzle_solved to true
							say `After a series of tapping sounds, the door to the {!!north|go north} swings open.`
					else
						do
							set _mural_sequence to 0
							say `You hear a distant clang.`

place named _house_laboratory looks like `To Be Continued...`
	do
		say `You enter the shadowy laboratory of Dr. Sinclair...`

thing named _house_blue_flower looks like `{!!an iridescent blue flower|_house_blue_flower}` is in _house_garden
	do
		if #command is `examine _house_blue_flower`
			say
				`This {flower|_house_blue_flower} glows with a strange blue light. The petals seem to twist and warp as if seen through a desert haze.`
		if #command is `use _house_blue_flower`
			if _house_blue_flower is in #here
				do
					say `You take the {!!flower.|_house_blue_flower}`
					move _house_blue_flower to #player

variable named _house_flower_beds_examined is false

place named _house_garden looks like `The back garden`
	do
		if #command is #enter
			or #command is `look around`
			then
				do
					say
						`This is the back garden of doctor Sinclar's house. <p>`
						`A {staggering array of flowers,|_house_flower_beds} most of which you don't recognize, fill the {orderly rows of beds|_house_flower_beds} that line a stone path here. <p>`
						`At the end of the path a park bench faces the house. <p>`
						`A gravel path leads around the side of the house to the {!!south.|go south}`
		if #command is `examine _house_blue_flower_found`
			if _house_blue_flower is in #here
				say `<p> A {!!strange blue flower|_house_blue_flower} nestled among the others catches your eye. <p>`
		if #command is `go south`
			travel to _street_sinclair_front_lawn
		if #command is `examine _house_flower_beds`
			do
				say `Red and yellow flowers, purple and {blue flowers.|_house_blue_flower_found} Thin needle-like petals, broad flat petals. A cacophony of colors and shapes assaults your senses.`
				if $_house_flower_beds_examined$ is false
					do
						set _house_flower_beds_examined to true
						say `Dr. Sinclar clearly has exotic taste in plantings. Yet you can't help feeling many of these are more than exotic. They seem alien, and unsettling.`
